apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation  'org.junit.jupiter:junit-jupiter-api:5.4.1'
    testImplementation  'org.junit.jupiter:junit-jupiter-params:5.4.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

group = 'com.github.zafarkhaja'
version = '0.11.0-SNAPSHOT'
description = 'Java implementation of the SemVer Specification, maintained by the AntiLaby Team'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
    from classes, processResources, file("LICENSE")
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
}

publishing {
    repositories {
        maven {
            url = mavenRepo
            credentials {
                username = mavenUsername
                password = mavenPassword
            }
            authentication {
                digest(BasicAuthentication)
            }

        }
    }
    publications {
        mavenJava(MavenPublication) {
            pom {
                description = project.description
                url = 'https://github.com/AntiLaby/jsemver'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'zafarkhaja'
                        name = 'Zafar Khaja'
                        email = 'zafarkhaja@gmail.com'
                        roles = ['developer']
                    }
                    developer {
                        id = 'heisluft'
                        name = 'heisluft'
                        email = 'heisluftlp@gmail.com'
                        roles = ['developer, maintainer']
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/AntiLaby/jsemver.git'
                    developerConnection = 'scm:git:ssh://git@github.com/AntiLaby/jsemver.git'
                    url = 'https://github.com/AntiLaby/jsemver'
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

test {
    useJUnitPlatform()
}

publishMavenJavaPublicationToMavenLocal.dependsOn test
publishMavenJavaPublicationToMavenRepository.dependsOn test